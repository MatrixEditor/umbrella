if(__add_umbrella_git)
  return()
endif()
set(__add_umbrella_git ON)

execute_process(
  COMMAND ${GIT_EXECUTABLE} log -1 --format=%h
  WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
  OUTPUT_VARIABLE UMBRELLA_COMMIT_HASH
  OUTPUT_STRIP_TRAILING_WHITESPACE
)

execute_process(
  COMMAND ${GIT_EXECUTABLE} rev-list --count ${UMBRELLA_COMMIT_HASH}
  WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
  OUTPUT_VARIABLE UMBRELLA_COMMIT_COUNT
  OUTPUT_STRIP_TRAILING_WHITESPACE
)

execute_process(
  COMMAND ${GIT_EXECUTABLE} describe --abbrev=0 --tags HEAD --always
  WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
  OUTPUT_VARIABLE UMBRELLA_GIT_TAG
  OUTPUT_STRIP_TRAILING_WHITESPACE
)

execute_process(
  COMMAND ${GIT_EXECUTABLE} tag --list --points-at=HEAD
  WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
  OUTPUT_VARIABLE UMBRELLA_GIT_COMMIT_TAGGED
  OUTPUT_STRIP_TRAILING_WHITESPACE
)

execute_process(
  COMMAND ${GIT_EXECUTABLE} rev-parse --abbrev-ref HEAD
  WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
  OUTPUT_VARIABLE UMBRELLA_GIT_BRANCH
  OUTPUT_STRIP_TRAILING_WHITESPACE
)

string(COMPARE NOTEQUAL "${UMBRELLA_GIT_COMMIT_TAGGED}" "" UMBRELLA_IS_TAGGED)

string(REGEX MATCHALL "([0-9]+)" VERSION_STRING "${UMBRELLA_GIT_TAG}")

message(STATUS "Tagged: ${UMBRELLA_IS_TAGGED}")
if (${UMBRELLA_IS_TAGGED})
  message(STATUS "Tag: ${UMBRELLA_GIT_TAG}")
else()
  if(UMBRELLA_GIT_BRANCH MATCHES "^release-")
    string(REGEX MATCHALL "([0-9]+)" VERSION_STRING "${UMBRELLA_GIT_BRANCH}")
    message(STATUS "${VERSION_STRING}")
  else()
    set(VERSION_STRING "0" "0" "1")
  endif()
endif()
message(STATUS "Current branch: ${UMBRELLA_GIT_BRANCH}")


if (VERSION_STRING)
  list(GET VERSION_STRING 0 UMBRELLA_VERSION_MAJOR)
  list(GET VERSION_STRING 1 UMBRELLA_VERSION_MINOR)
  list(GET VERSION_STRING 2 UMBRELLA_VERSION_PATCH)

  if (NOT ${UMBRELLA_IS_TAGGED})
    if(UMBRELLA_GIT_BRANCH MATCHES "^release-")
      message(STATUS "Release branch")
    else()
      MATH(EXPR UMBRELLA_VERSION_MINOR "${UMBRELLA_VERSION_MINOR}+1")
      set(UMBRELLA_VERSION_PATCH 0)
    endif()
  endif()
else()
  set(UMBRELLA_VERSION_MAJOR 0)
  set(UMBRELLA_VERSION_MINOR 0)
  set(UMBRELLA_VERSION_PATCH 0)
endif()

